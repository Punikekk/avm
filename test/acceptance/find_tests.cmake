function(find_tests_at add_function glob)
    file(GLOB tests ${glob})
    if (NOT tests)
        return()
    endif ()

    file(RELATIVE_PATH target_name ${AS3_BASEDIR} ${dir})
    if (target_name)
        string(REPLACE "/" "_" target_name "${AS3_PREFIX}_${target_name}")
        if (NOT TARGET ${target_name})
            add_custom_target(${target_name})
            add_dependencies(${AS3_TARGET} ${target_name})
        endif()
        set(AS3_TARGET ${target_name})
    endif()

    foreach (test ${tests})
        if (test IN_LIST AS3_IGNORE)
            continue()
        endif()
        get_filename_component(src ${test} NAME)
        get_filename_component(src_name ${test} NAME_WLE)
        get_filename_component(src_dir ${test} DIRECTORY)
        set(args ${test})
        get_property(file_args SOURCE ${test} PROPERTY AS3_ARGUMENTS)
        if (file_args)
            list(APPEND args ${file_args})
        endif ()
        file(GLOB support ${src_dir}/${src_name}/*.as)
        if (support)
            list(LENGTH support support_length)
            if (support_length GREATER 1)
                foreach (sup_src ${support})
                    file(RELATIVE_PATH sup_src ${src_dir}/${src_name} ${sup_src})
                    if (NOT sup_src MATCHES "^[A-Z]_")
                        message(WARNING "Support code at ${src_dir}/${src_name} may not be ordered correctly")
                        break()
                    endif ()
                endforeach ()
            endif ()
            list(APPEND args SUPPORT ${support})
        endif ()
        if (add_function STREQUAL as3_add_test)
            as3_add_test(${args})
        elseif(add_function STREQUAL abs_add_test)
            abs_add_test(${args})
        else()
            message(WARNING "Unknown add function ${add_function}")
        endif()
    endforeach ()
endfunction()

function(find_tests name glob add_function ext_glob)
    if (NOT TARGET ${name})
        add_custom_target(${name})
        add_dependencies(${AS3_TARGET} ${name})
    endif()
    set(AS3_TARGET ${name})
    file(GLOB dirs ${glob} LIST_DIRECTORIES true)
    foreach (dir ${dirs})
        if (IS_DIRECTORY ${dir})
            find_tests_at(${add_function} ${dir}/${ext_glob})
        endif ()
    endforeach ()
endfunction()

macro(as3_find_tests name glob)
    find_tests(${name} ${glob} as3_add_test *.as)
endmacro()

macro(abs_find_tests name glob)
    find_tests(${name} ${glob} abs_add_test *.abs)
endmacro()

function(set_property_source_glob glob)
    file(GLOB_RECURSE files ${glob})
    foreach (file ${files})
        set_property(SOURCE ${file} ${ARGN})
    endforeach ()
endfunction()

function(as3_set_arguments glob)
    set_property_source_glob(${glob} PROPERTY AS3_ARGUMENTS ${ARGN})
endfunction()

function(as3_add_arguments glob)
    set_property_source_glob(${glob} APPEND PROPERTY AS3_ARGUMENTS ${ARGN})
endfunction()

function(as3_ignore glob)
    file(GLOB_RECURSE files ${glob})
    set(AS3_IGNORE ${AS3_IGNORE} ${files} PARENT_SCOPE)
endfunction()