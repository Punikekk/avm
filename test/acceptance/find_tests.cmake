function(find_tests_at add_function glob)
    file(GLOB tests ${glob})
    if (NOT tests)
        return()
    endif ()

    file(RELATIVE_PATH test_name ${AS3_BASEDIR} ${dir})
    string(REPLACE "/" "_" test_name "test_${test_name}")
    if (NOT TARGET ${test_name})
        add_custom_target(${test_name})
        add_dependencies(${AS3_TARGET} ${test_name})
    endif()
    set(AS3_TARGET ${test_name})

    foreach (test ${tests})
        get_filename_component(src ${test} NAME)
        get_filename_component(src_name ${test} NAME_WLE)
        get_filename_component(src_dir ${test} DIRECTORY)
        set(args ${test})
        get_property(skip SOURCE ${test} PROPERTY AS3_IGNORE_THIS)
        get_property(file_args SOURCE ${test} PROPERTY AS3_ARGUMENTS)
        if (skip)
            continue()
        endif ()
        if (file_args)
            list(APPEND args ${file_args})
        endif ()
        file(GLOB support ${src_dir}/${src_name}/*.as)
        if (support)
            list(LENGTH support support_length)
            if (support_length GREATER 1)
                foreach (sup_src ${support})
                    file(RELATIVE_PATH sup_src ${src_dir}/${src_name} ${sup_src})
                    if (NOT sup_src MATCHES "^[A-Z]_")
                        message(WARNING "Support code at ${src_dir}/${src_name} may not be ordered correctly")
                        break()
                    endif ()
                endforeach ()
            endif ()
            list(APPEND args SUPPORT ${support})
        endif ()
        if (add_function STREQUAL as3_add_test)
            as3_add_test(${args})
        elseif(add_function STREQUAL abs_add_test)
            abs_add_test(${args})
        else()
            message(WARNING "Unknown add function ${add_function}")
        endif()
    endforeach ()
endfunction()

function(find_tests name glob add_function ext_glob)
    if (NOT TARGET ${name})
        add_custom_target(${name})
        add_dependencies(${AS3_TARGET} ${name})
    endif()
    set(AS3_TARGET ${name})
    file(GLOB dirs ${glob} LIST_DIRECTORIES true)
    foreach (dir ${dirs})
        if (IS_DIRECTORY ${dir})
            find_tests_at(${add_function} ${dir}/${ext_glob})
        endif ()
    endforeach ()
endfunction()

macro(as3_find_tests name glob)
    find_tests(${name} ${glob} as3_add_test *.as)
endmacro()

macro(abs_find_tests name glob)
    find_tests(${name} ${glob} abs_add_test *.abs)
endmacro()

function(set_src_properties glob property)
    file(GLOB_RECURSE files ${glob})
    foreach (file ${files})
        set_property(SOURCE ${file} PROPERTY ${property} ${ARGN})
    endforeach ()
endfunction()

function(as3_set_arguments glob)
    set_src_properties(${glob} AS3_ARGUMENTS ${ARGN})
endfunction()

function(as3_ignore glob)
    set_src_properties(${glob} AS3_IGNORE_THIS 1)
endfunction()